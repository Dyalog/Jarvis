 r←OnWsReceive tns;payload;cns;j;endpoint;res;resp
 ⍝ tns is the connection's namespace for this thread (message)
 r←0
 payload←⎕JSON tns.Payload
 cns←tns.##   ⍝ parent connection namespace
 j←cns.Server ⍝ ref to the Jarvis server
 :Select {6::⍵ ⋄ payload.Type}''
 :Case 'Endpoint'
     endpoint←(⍕j.CodeLocation),'.'@(=∘'/')⊢payload.Endpoint
     :Trap 0
         res←(⍎endpoint)payload.Payload
     :Else
         res←⎕DMX.EM,' occurred on call to "',endpoint,'"'
     :EndTrap
     resp←⎕NS''
     resp.Type←'Result'
     resp.Data←res
     cns j.WsSend resp
 :Case 'Subscribe'
     Subscribe cns.conx
 :Case 'Unsubscribe'
     Unsubscribe cns.conx
     resp←⎕NS''
     resp.Type←'Update'
     resp.Data←'Unsubscribed'
     cns cns.Server.WsSend resp
 :Else
     ⎕←'Unknown payload type'
     ⎕←⎕JSON payload
 :EndSelect
